 R←Test_Move_009(stopFlag batchFlag);⎕TRAP;myTempDir;filenames;rc;more
⍝ Exercise `MoveTo`: move three files into three different directories.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 myTempDir←##.FilesAndDirs.GetTempPath,(⍕##.OS.GetPID),'_',(' '~⍨⍕⎕TS),'/'
 ##.FilesAndDirs.MkDir myTempDir
 filenames←{'foo_',(⍕⍵),'.txt'}¨1+⍳3
 ##.FilesAndDirs.NPUT{(⊂⍵)⍺⍺ ⍵}¨myTempDir∘,¨filenames
 ##.FilesAndDirs.MkDir myTempDir,'Sub/'
 {##.FilesAndDirs.MkDir myTempDir,'Sub/',⍵}¨⍕¨1+⍳3
 (rc more)←(myTempDir∘,¨filenames)##.FilesAndDirs.MoveTo(⊂myTempDir,'Sub/'),¨(⍕¨1+⍳3),¨'/'
 →T.GoToTidyUp 3≠+/##.FilesAndDirs.IsFile¨filenames{myTempDir,'Sub/',⍵,'/',⍺}¨⍕¨1+⍳3
 →T.GoToTidyUp 0≠+/##.FilesAndDirs.IsFile¨(⊂myTempDir),¨filenames

 R←T._OK

∆TidyUp:
 {}##.FilesAndDirs.RmDir myTempDir
