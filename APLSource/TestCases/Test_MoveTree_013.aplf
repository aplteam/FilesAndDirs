 R←Test_MoveTree_013(stopFlag batchFlag);⎕TRAP;rc;more;list;successFlag;success;tempPathSource;tempPathTarget;filename;tno;record;en;fqn;row;noOf2;sourceList;targetList;F;noOf_1a;noOf_1b
⍝ Exercise `MoveTree`: move a tree of dirs and files with one file tied so that is can be copied but not deleted.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed
 :If 'Win'≢##.APLTreeUtils.GetOperatingSystem ⍬
     R←T._WindowsOnly
     :Return
 :Else
     F←##.FilesAndDirs
     tempPathSource←F.GetTempPath,'source_',↑⎕SI
     tempPathTarget←F.GetTempPath,'target_',↑⎕SI
     (rc en more)←F.RmDir tempPathSource
     Assert 0=rc
     (rc en more)←F.RmDir tempPathTarget
     Assert 0=rc
     ∆CreateDirsAndFiles tempPathSource
     filename←F.NormalizePath'Second/Sub_a/nativefile.txt'
     fqn←F.NormalizePath tempPathSource,'/',filename   ⍝ fully qualified name
     tno←fqn ⎕NCREATE 0
     'Hello world'⎕NAPPEND tno
     ⎕NUNTIE tno
     tno←fqn ⎕NTIE 0 32    ⍝ Grant read access to subsequent users; thats allows a copy operation but prevents the file from bing deleted
     noOf_1a←≢sourceList←↑('recursive' 1)F.Dir tempPathSource,'\'
     (success more list)←tempPathSource F.MoveTree tempPathTarget
     →T.GoToTidyUp 0≠success
     →T.GoToTidyUp 0≠≢more
     noOf2←≢targetList←↑('recursive' 1)F.Dir tempPathTarget,'\'
     noOf_1b←≢sourceList←↑('recursive' 1)F.Dir tempPathSource,'\'
     →T.GoToTidyUp noOf_1a≠noOf2          ⍝ Copy worked!
     →T.GoToTidyUp noOf_1b≠3              ⍝ But deleting one file and its parent directories failed!
     row←filename(##.APLTreeUtils.Lowercase{↑##.APLTreeUtils.Where((-≢⍺)↑¨⍺⍺ ⍵)≡¨⊂⍺⍺ ⍺})list[;0]
     →T.GoToTidyUp 0=≢2⊃list[row;]

     R←T._OK
 :EndIf
∆TidyUp:
 :Trap 0 ⋄ ⎕NUNTIE tno ⋄ :EndTrap
 :Trap 0
     {}F.RmDir tempPathSource
     {}F.RmDir tempPathTarget
 :EndTrap
