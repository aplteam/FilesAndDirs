 R←Test_Misc_021(stopFlag batchFlag);⎕TRAP;folder;path;tempSubDir;dmx
⍝ Test the `GetTempSubFolder` method
⍝ See also the test for the `GetTempFilename` method.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 tempSubDir←##.FilesAndDirs.GetTempSubDir''
 (path folder)←2↑⎕NPARTS tempSubDir
 →T.GoToTidyUp path≢##.FilesAndDirs.GetTempPath
 →T.GoToTidyUp ⎕AN≢⊃'_'APLTreeUtils2.Split folder
 →T.GoToTidyUp 1≠##.FilesAndDirs.IsDir tempSubDir
 ##.FilesAndDirs.RmDir tempSubDir

 tempSubDir←##.FilesAndDirs.GetTempSubDir'Foo\'
 (path folder)←2↑⎕NPARTS tempSubDir
 →T.GoToTidyUp path≢##.FilesAndDirs.GetTempPath
 →T.GoToTidyUp'Foo'≢⊃'_'APLTreeUtils2.Split folder
 →T.GoToTidyUp 1≠##.FilesAndDirs.IsDir tempSubDir
 ##.FilesAndDirs.RmDir tempSubDir

 :Trap 22
     tempSubDir←##.FilesAndDirs.GetTempSubDir'***'
     →T.GoToTidyUp 1
 :Else
     dmx←⎕DMX
     →T.GoToTidyUp'Unable to create '{⍺≢(≢⍺)↑⍵}dmx.EM
 :EndTrap

 R←T._OK

∆TidyUp:
 :Trap 0 ⋄ {}##.FilesAndDirs.RmDir tempSubDir ⋄ :EndTrap
⍝Done
