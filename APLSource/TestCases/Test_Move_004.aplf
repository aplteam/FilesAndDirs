 R←Test_Move_004(stopFlag batchFlag);⎕TRAP;myTempDir;filename;rc;more
⍝ Exercise `MoveTo`: move a file to a folder and give it a new name, then retry.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 myTempDir←##.FilesAndDirs.GetTempPath,(⍕##.OS.GetPID),'_',(' '~⍨⍕⎕TS),'/'
 ##.FilesAndDirs.MkDir myTempDir
 filename←myTempDir,↑⎕SI
 ##.APLTreeUtils.WriteUtf8File filename(↑⎕SI)
 ##.FilesAndDirs.MkDir myTempDir,'TargetDir'
 (rc more)←filename ##.FilesAndDirs.MoveTo myTempDir,'TargetDir/newname'
 →T.GoToTidyUp 0≠rc
 →T.GoToTidyUp 0=##.FilesAndDirs.IsFile myTempDir,'TargetDir/newname'
 →T.GoToTidyUp 0≠##.FilesAndDirs.IsFile filename
 ##.APLTreeUtils.WriteUtf8File filename'Overwritten'
 (rc more)←filename ##.FilesAndDirs.MoveTo myTempDir,'TargetDir/newname'
 →T.GoToTidyUp 0≠rc
 →T.GoToTidyUp 0=##.FilesAndDirs.IsFile myTempDir,'TargetDir/newname'
 →T.GoToTidyUp 0≠##.FilesAndDirs.IsFile filename
 →T.GoToTidyUp'Overwritten'≢##.APLTreeUtils.ReadUtf8File myTempDir,'TargetDir/newname'

 R←T._OK

∆TidyUp:
 {}##.FilesAndDirs.RmDir myTempDir
