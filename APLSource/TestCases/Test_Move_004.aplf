 R←Test_Move_004(stopFlag batchFlag);⎕TRAP;myTempDir;filename;rc;more
⍝ Exercise `MoveTo`: move a file to a folder and give it a new name, then retry.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 myTempDir←##.FilesAndDirs.GetTempPath,(⍕##.OS.GetPID),'_',(' '~⍨⍕⎕TS),'/'
 ##.FilesAndDirs.MkDir myTempDir
 filename←myTempDir,⊃⎕SI
 ⎕SI[1]##.FilesAndDirs.NPUT filename
 ##.FilesAndDirs.MkDir myTempDir,'TargetDir'
 (rc more)←filename ##.FilesAndDirs.MoveTo myTempDir,'TargetDir/newname'
 →T.GoToTidyUp 0≠rc
 →T.GoToTidyUp 0=##.FilesAndDirs.IsFile myTempDir,'TargetDir/newname'
 →T.GoToTidyUp 0≠##.FilesAndDirs.IsFile filename
 (⊂'Overwritten')##.FilesAndDirs.NPUT filename 1
 (rc more)←filename ##.FilesAndDirs.MoveTo myTempDir,'TargetDir/newname'
 →T.GoToTidyUp 0≠rc
 →T.GoToTidyUp 0=##.FilesAndDirs.IsFile myTempDir,'TargetDir/newname'
 →T.GoToTidyUp 0≠##.FilesAndDirs.IsFile filename
 →T.GoToTidyUp'Overwritten'≢⊃##.FilesAndDirs.NGET myTempDir,'TargetDir/newname'

 R←T._OK

∆TidyUp:
 {}##.FilesAndDirs.RmDir myTempDir
