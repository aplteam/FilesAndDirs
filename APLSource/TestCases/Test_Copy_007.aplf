 R←Test_Copy_007(stopFlag batchFlag);⎕TRAP;myTempDir;filenames;more;rc
⍝ Exercise `CopyTo`: Copy 3 files into a single dir
⍝ This test makes sense under Windows only
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 R←T._Failed

 myTempDir←##.FilesAndDirs.GetTempPath,(⍕##.OS.GetPID),'_',(' '~⍨⍕⎕TS),'/'
 ##.FilesAndDirs.MkDir myTempDir
 filenames←{'foo_',(⍕⍵),'.txt'}¨1+⍳3
 ##.FilesAndDirs.NPUT{(⊂⍵)⍺⍺ ⍵}¨myTempDir∘,¨filenames
 ##.FilesAndDirs.MkDir myTempDir,'Sub/'
 (rc more)←(myTempDir∘,¨filenames)##.FilesAndDirs.CopyTo myTempDir,'Sub/'
 →T.GoToTidyUp 1∨.≠##.FilesAndDirs.IsFile¨(⊂myTempDir,'Sub/'),¨filenames
 →T.GoToTidyUp 1∨.≠##.FilesAndDirs.IsFile¨(⊂myTempDir),¨filenames

 R←T._OK

∆TidyUp:
 {}##.FilesAndDirs.RmDir myTempDir
