 R←Test_Move_005(stopFlag batchFlag);⎕TRAP;filename;rc;more;ini;targetDir;sourceDir
⍝ Exercise `MoveTo` (Windows only): try to move from one drive to another
⍝ Requires an INI file and appropriate settings.
⍝ Assumes that Windows is installed on the C:\ drive and that
⍝ [Config]:FolderOnSecondDrive points to a different drive,
⍝ otherwise the test is pointless.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 :If 'Win'≢##.APLTreeUtils2.GetOperatingSystem ⍬
     R←T._WindowsOnly
 :ElseIf 0=⎕NEXISTS ∆GetComputerName,'.ini'
     ⎕←'No INI file found for "',(∆GetComputerName),'"'
     R←T._OK
 :Else
     ini←⎕NEW ##.IniFiles(,⊂∆GetComputerName,'.ini')
     sourceDir←##.FilesAndDirs.GetTempPath,('/',⍕##.OS.GetPID),'_',(' '~⍨⍕⎕TS),'/'
     ##.FilesAndDirs.MkDir sourceDir
     filename←sourceDir,⊃⎕SI
     ⎕SI[1]##.FilesAndDirs.NPUT filename
     targetDir←##.FilesAndDirs.NormalizePath(↑ini.Get'Config:FolderOnSecondDrive'),('/',⍕##.OS.GetPID),'_',(' '~⍨⍕⎕TS),'/'
     ##.FilesAndDirs.MkDir targetDir
     (rc more)←filename ##.FilesAndDirs.MoveTo targetDir
     →T.GoToTidyUp 0≠rc
     →T.GoToTidyUp 0=##.FilesAndDirs.IsFile targetDir,⊃⎕SI
     →T.GoToTidyUp 0≠##.FilesAndDirs.IsFile filename
     (⊂'Overwritten')##.FilesAndDirs.NPUT filename 1
     (rc more)←filename ##.FilesAndDirs.MoveTo targetDir
     →T.GoToTidyUp 0≠rc
     →T.GoToTidyUp 0=##.FilesAndDirs.IsFile targetDir,⊃⎕SI
     →T.GoToTidyUp 0≠##.FilesAndDirs.IsFile filename
     →T.GoToTidyUp'Overwritten'≢⊃##.FilesAndDirs.NGET targetDir,⊃⎕SI

     R←T._OK

∆TidyUp:
     {}##.FilesAndDirs.RmDir targetDir

 :EndIf
