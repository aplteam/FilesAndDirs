 R←Test_Copy_002(stopFlag batchFlag);⎕TRAP;myTempDir;filename;rc;more
⍝ Exercise `CopyTo`: copy a file to a folder with a new name
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 myTempDir←##.FilesAndDirs.GetTempPath,(⍕##.OS.GetPID),'_',(' '~⍨⍕⎕TS),'/'
 ##.FilesAndDirs.MkDir myTempDir
 filename←myTempDir,↑⎕SI
 ##.APLTreeUtils.WriteUtf8File filename(↑⎕SI)
 ##.FilesAndDirs.MkDir myTempDir,'TargetDir'
 (rc more)←filename ##.FilesAndDirs.CopyTo myTempDir,'TargetDir/NewName'
 →T.GoToTidyUp 0=##.FilesAndDirs.IsFile myTempDir,'TargetDir/NewName'

 R←T._OK

∆TidyUp:
 {}##.FilesAndDirs.RmDir myTempDir
