 R←Test_Copy_008(stopFlag batchFlag);⎕TRAP;myTempDir;filenames;rc;more;targetDirs
⍝ Exercise `CopyTo`: Copy 3 files into three different directories
⍝ This test makes sense under Windows only
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 R←∆Failed

 myTempDir←##.FilesAndDirs.GetTempPath,(⍕#.OS.GetPID),'_',(' '~⍨⍕⎕TS),'/'
 ##.FilesAndDirs.MkDir myTempDir
 filenames←{'foo_',(⍕⍵),'.txt'}¨1+⍳3
 myTempDir∘{#.APLTreeUtils.WriteUtf8File(⍺,⍵)⍵}¨filenames
 targetDirs←(⊂myTempDir,'Sub/'),¨⍕¨1+⍳3
 {'Create!'##.FilesAndDirs.CheckPath ⍵}¨targetDirs
 (rc more)←(myTempDir∘,¨filenames)##.FilesAndDirs.CopyTo targetDirs,¨'/'
 →GoToTidyUp 1∨.≠##.FilesAndDirs.IsFile¨(⊂myTempDir,'Sub/'),¨('/',⍨¨⍕¨1+⍳3),¨filenames
 →GoToTidyUp 1∨.≠##.FilesAndDirs.IsFile¨(⊂myTempDir),¨filenames

 R←∆OK

∆TidyUp:
 {}##.FilesAndDirs.RmDir myTempDir
