 R←Test_Copy_005(stopFlag batchFlag);⎕TRAP;myTempDir;filename;rc;more
⍝ Exercise `CopyTo`: copy a file to a folder with a new bad invalid filename
⍝ This test makes sense under Windows only: there are no invalid filenames under Linux
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 :If 'Win'≡##.APLTreeUtils2.GetOperatingSystem ⍬
     R←T._Failed

     myTempDir←##.FilesAndDirs.GetTempPath,(⍕##.OS.GetPID),'_',(' '~⍨⍕⎕TS),'/'
     ##.FilesAndDirs.MkDir myTempDir
     filename←myTempDir,⊃⎕SI
     ⎕SI[1]##.FilesAndDirs.NPUT filename
     (rc more)←filename ##.FilesAndDirs.CopyTo myTempDir,'TargetDir/***'
     →T.GoToTidyUp 0=rc

     R←T._OK

∆TidyUp:
     {}##.FilesAndDirs.RmDir myTempDir

 :Else
     R←T._WindowsOnly
 :EndIf
