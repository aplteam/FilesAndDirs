 R←Test_DeleteFile_001(stopFlag batchFlag);⎕TRAP;myTempPath;success;filename
⍝ Test the `DeleteFile` method on an existent file.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←∆Failed

 myTempPath←##.FilesAndDirs.GetTempPath,↑⎕SI
 {}##.FilesAndDirs.RmDir myTempPath ⍝ Get rid of any leftovers
 success←##.FilesAndDirs.MkDir myTempPath
 filename←myTempPath,'\',↑⎕SI
 #.APLTreeUtils.WriteUtf8File filename'Hello world'
 Assert ##.FilesAndDirs.IsFile filename
 success←##.FilesAndDirs.DeleteFile filename
 →GoToTidyUp 1≠success
 →GoToTidyUp 0≠##.FilesAndDirs.IsFile filename
 →GoToTidyUp 0≠##.FilesAndDirs.Exists filename

 R←∆OK

∆TidyUp:
 {}##.FilesAndDirs.RmDir myTempPath
