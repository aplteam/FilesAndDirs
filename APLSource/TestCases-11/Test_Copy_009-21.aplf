 R←Test_Copy_009(stopFlag batchFlag);⎕TRAP;myTempDir;filenames;more;rc
⍝ Exercise `CopyTo`: Copy 3 files into a single dir with one of them not really existing
⍝ This test makes sense under Windows only
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 R←∆Failed

 myTempDir←##.FilesAndDirs.GetTempPath,(⍕#.OS.GetPID),'_',(' '~⍨⍕⎕TS),'/'
 ##.FilesAndDirs.MkDir myTempDir
 filenames←{'foo_',(⍕⍵),'.txt'}¨1+⍳3
 #.APLTreeUtils.WriteUtf8File{⍺⍺ ⍵ ⍵}¨myTempDir∘,¨1 0 1/filenames
 ##.FilesAndDirs.MkDir myTempDir,'Sub/'
 (rc more)←(myTempDir∘,¨filenames)##.FilesAndDirs.CopyTo myTempDir,'Sub/'
 →GoToTidyUp 2≠+/##.FilesAndDirs.IsFile¨(⊂myTempDir,'Sub/'),¨filenames
 →GoToTidyUp 2≠+/0=rc
 →GoToTidyUp 2≠+/0=↑¨⍴¨more

 R←∆OK

∆TidyUp:
 {}##.FilesAndDirs.RmDir myTempDir
