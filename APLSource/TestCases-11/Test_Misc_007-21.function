 R←Test_Misc_007(stopFlag batchFlag);⎕TRAP;parms;tempFile;myTempDir
⍝ Test the `CheckPath` method
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←∆Failed

 myTempDir←##.FilesAndDirs.GetTempPath,(⍕#.OS.GetPID),'_',' '~⍨⍕⎕TS
 :If ##.FilesAndDirs.IsDir myTempDir
 :AndIf 1≠↑##.FilesAndDirs.RmDir myTempDir
     .⍝ Something is wrong
 :EndIf
 →PassesIf 0=##.FilesAndDirs.CheckPath myTempDir
 →PassesIf 1='Create!'##.FilesAndDirs.CheckPath myTempDir
 myTempDir,←'aaa/bb/c'
 →PassesIf 1='Create!'##.FilesAndDirs.CheckPath myTempDir
 →PassesIf ##.FilesAndDirs.IsDir myTempDir

 :If 'Win'≡#.APLTreeUtils.GetOperatingSystem ⍬
     ⍝ Use an invalid filename in order to provoke an error
     →PassesIf 0='Create!'##.FilesAndDirs.CheckPath myTempDir,'???'
 :EndIf

 R←∆OK

∆TidyUp:
 :Trap 0
     {}##.FilesAndDirs.RmDir myTempDir
 :EndTrap
